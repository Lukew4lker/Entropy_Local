;MEMORY MANAGER by Lucus
;Implements dynamic,
;resizable memory.

;Section header:
;0x0 free space after this
;0x1 location of next section

;Expects user to call pm_lock

;A=number of words
;return A=section location or
;-1 if out of space
:mm_request
set push, b
set push, c
;find space
add a, 1 ;account for header
set b, 0
:.loop_a
ifg [b], a
set pc, .found_space
set b, [b + 1]
ifn b, -1
set pc, .loop_a
;no space
set a, -1
set c, pop
set b, pop
set pc, pop
:.found_space
;B=start of section header
add a, 1
set c, [b]
sub c, a
;C=new free space
mul a, -1
add a, [b + 1]
;A=new header location
set [b], c
set [a + 1], [b + 1]
set [b + 1], a
set [a], 0
add a, 2 ;account for header
set c, pop
set b, pop
set pc, pop

;A=location of section
;return A=success or errno
:mm_free
set push, b
sub a, 2
set b, 0
:.loop_a
ife [b + 1], a
set pc, .found_it
set b, [b + 1]
ifn b, -1
set pc, .loop_a
;section does not exist
set a, err_does_not_exist
set b, pop
set pc, pop
:.found_it
;B=section before target
add [b], [a + 1]
sub [b], a
set [b + 1], [a + 1]
set a, 0 ;success
set b, pop
set pc, pop

;A=location of section
;B=words to shrink
:mm_shrink
mul b, -1
;continue in mm_grow

;A=location of section
;B=words to grow
:mm_grow
add b, [a - 1]
sub b, [a - 2]
sub b, a
sub b, 2
;continue in mm_resize

;A=location of section
;B=target size
;return A=success or errno
:mm_resize
set push, c
sub a, 2
add b, 2
set c, [a + 1]
sub c, a
ifg b, c
set pc, .no_space
sub c, b
;C=new free space
set [a], c
set a, 0 ;success
set c, pop
set pc, pop
:.no_space
set a, err_out_of_space
set c, pop
set pc, pop

